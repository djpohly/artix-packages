# Maintainer: Chris Cromer <cromer@artixlinux.org>

pkgname=pamac
_pkgver=6.2.4
pkgver=$_pkgver
pkgrel=1
pkgdesc="A Gtk3 frontend for libalpm"
arch=('i686' 'x86_64')
url="https://github.com/manjaro/pamac"
license=('GPL3')
depends=('glib2>=2.42' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3>=0.38' 'gtk3>=3.22'
         'libnotify' 'desktop-file-utils' 'pacman>=5.0' 'pacman<5.1' 'gnutls>=3.4'
         'appstream-glib' 'archlinux-appstream-data')
makedepends=('gettext' 'itstool' 'vala>=0.36' 'meson' 'ninja')
conflicts=('pamac-tray-appindicator')

source=("pamac-$pkgver.tar.gz::$url/archive/v$_pkgver.tar.gz")
sha256sums=('92e734abd1a3bec26571545edc2a96cf0340f2c3f493a21b508e80310bf4bf74')

prepare() {
  cd "$srcdir/pamac-$_pkgver"
  # patches here

  # adjust version string
  sed -i -e "s|\"$_pkgver\"|\"$pkgver-$pkgrel\"|g" src/manager_window.vala

  [ -d build ] || mkdir build
}

build() {
  cd "$srcdir/pamac-${_pkgver}/build"
  meson --prefix=/usr --sysconfdir=/etc
  ninja
}

package() {
  optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome'
			  'mate-polkit: needed for authentification in Mate'
              'lxsession: needed for authentification in Xfce, LXDE etc.'
              'pamac-tray-appindicator: tray icon for KDE')
  backup=('etc/pamac.conf')
  replaces=('pamac-aur')
  install=pamac.install
  cd "$srcdir/pamac-$_pkgver/build"
  DESTDIR="$pkgdir" ninja install
  rm -rf "${pkgdir}/etc/systemd"
  rm -rf "${pkgdir}/usr/lib/systemd"
}
# vim:set ts=2 sw=2 et:
