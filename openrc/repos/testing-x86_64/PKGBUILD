# Maintainer: artoo <artoo@artixlinux.org>
# Maintainer: Chris Cromer <cromer@artixlinux.org>
# Contributor: williamh <williamh@gentoo.org>

_url="https://github.com/OpenRC/openrc/archive"

pkgbase=openrc
pkgname=('openrc' 'openrc-bash-completions' 'openrc-zsh-completions')
pkgver=0.35
pkgrel=1
arch=('i686' 'x86_64' 'armv7h')
url="http://www.gentoo.org/proj/en/base/openrc/"
license=('BSD2')
makedepends=('psmisc' 'pam' 'opentmpfiles')
optdepends=('netifrc: Network Interface Management Scripts'
            'networkmanager-openrc: NetworkManager')
conflicts=('systemd-sysvcompat' 'sysvinit')
replaces=('sysvinit')
options=('!emptydirs')
source=("${pkgbase}-${pkgver}.tar.gz::${_url}/${pkgver}.tar.gz"
        "${pkgbase}.logrotate"
        'openrc-install.hook'
        'openrc-remove.hook')
sha256sums=('6b0e0e3cbe9aa888cfc1408a708f0e881cf99fe283cc08ed946540dc4e407e2d'
            '0b44210db9770588bd491cd6c0ac9412d99124c6be4c9d3f7d31ec8746072f5c'
            '1d4d31a7275660e7b23778e6fceb0714cab8ed2793b04aa004b01d8e7bcd43bd'
            'cbb4d00262a55a508114cb31e3903350be2cff748a4fb5e120dc745292e3987e')

_args=(
    SYSCONFDIR=/etc
    PREFIX=/usr
    SBINDIR=/usr/bin
    LIBEXECDIR=/usr/lib/openrc
    MKSELINUX=no
    MKPAM=pam
    MKTERMCAP=ncurses
    MKNET=no
    MKSYSVINIT=yes
    MKBASHCOMP=yes
    MKZSHCOMP=yes
)

if [ -f /usr/lib/os-release ]; then
    . /usr/lib/os-release
    _args+=(BRANDING="${NAME}")
else
    _args+=(BRANDING='Unknown Linux')
fi

prepare(){
    cd "${srcdir}/${pkgbase}-${pkgver}"
    sed -e "s|/sbin|/usr/bin|g" -i support/sysvinit/inittab
    sed -i 's:0444:0644:' mk/sys.mk
}

build(){
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make "${_args[@]}"
}

package_openrc() {
	depends=('psmisc' 'pam' 'opentmpfiles')
	pkgdesc="Gentoo's universal init system"
	install=${pkgname}.install
	backup=('etc/rc.conf'
			'etc/conf.d/consolefont'
			'etc/conf.d/keymaps'
			'etc/conf.d/hostname'
			'etc/conf.d/modules'
			'etc/conf.d/hwclock'
			etc/conf.d/agetty.tty{1,2,3,4,5,6})
    cd "${srcdir}/${pkgbase}-${pkgver}"

    make DESTDIR="${pkgdir}" "${_args[@]}" install

    install -Dm644 "${srcdir}/${pkgbase}.logrotate" "${pkgdir}/etc/logrotate.d/${pkgbase}"

    sed -e 's/#unicode="NO"/unicode="YES"/' \
        -e 's/#rc_logger="NO"/rc_logger="YES"/' \
        -i "${pkgdir}/etc/rc.conf"

    install -d ${pkgdir}/usr/lib/openrc/cache

    # getty
    sed -e 's|#baud=""|baud="38400"|' \
        -e 's|#term_type="linux"|term_type="linux"|' \
        -e 's|#agetty_options=""|agetty_options=""|' \
        -i ${pkgdir}/etc/conf.d/agetty

    for num in 1 2 3 4 5 6;do
        cp -v ${pkgdir}/etc/conf.d/agetty ${pkgdir}/etc/conf.d/agetty.tty$num
        ln -sf /etc/init.d/agetty ${pkgdir}/etc/init.d/agetty.tty$num
        ln -sf /etc/init.d/agetty.tty$num ${pkgdir}/etc/runlevels/default/agetty.tty$num
    done

    # alpm hooks
    for h in openrc-{install,remove}.hook;do
        install -Dm644 ${srcdir}/$h ${pkgdir}/usr/share/libalpm/hooks/$h
    done

    # license
    install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LICENSE "$pkgdir/usr/share/licenses/${pkgbase}/LICENSE"

    # split completions
    rm -rf "${pkgdir}"/usr/share/{bash-completion,zsh}
}

package_openrc-bash-completions() {
	depends=('openrc' 'bash')
	pkgdesc="OpenRC completions for Bash"
    cd "${srcdir}/${pkgbase}-${pkgver}"
	make DESTDIR="${pkgdir}" "${_args[@]}" install
	rm -rf "${pkgdir}"/etc
	rm -rf "${pkgdir}"/usr/{bin,include,lib}
	rm -rf "${pkgdir}"/usr/share/{libalpm,licenses,man,openrc,zsh}
}

package_openrc-zsh-completions() {
	depends=('openrc' 'zsh')
	pkgdesc="OpenRC completions for ZSH"
    cd "${srcdir}/${pkgbase}-${pkgver}"
	make DESTDIR="${pkgdir}" "${_args[@]}" install
	rm -rf "${pkgdir}"/etc
	rm -rf "${pkgdir}"/usr/{bin,include,lib}
	rm -rf "${pkgdir}"/usr/share/{libalpm,licenses,man,openrc,bash-completion}
}
